import org.apache.tools.ant.taskdefs.condition.Os

import static org.gradle.api.JavaVersion.VERSION_11

apply plugin: 'kotlin'
apply plugin: 'idea'


repositories {
    maven { url "http://ci-artifactory.corda.r3cev.com/artifactory/freighter-dev" }
}

sourceSets {
    freighterTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/freighterTest/kotlin')
        }
    }
}

evaluationDependsOn(":workflows")
task freighterTest(type: Test, dependsOn: [project(":workflows").jar]) {
    maxParallelForks 2
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "128"
    testClassesDirs = sourceSets.freighterTest.output.classesDirs
    classpath = sourceSets.freighterTest.runtimeClasspath
    useJUnitPlatform {
        includeTags "DOCKER"
        excludeTags "AZURE", "ORACLE"
        if (Os.isFamily(Os.FAMILY_WINDOWS) || Os.isFamily(Os.FAMILY_MAC)) {
            //windows + mac cannot run comcast tests and cannot run oracle tests due to credential store issues
            excludeTags "FULL_LINUX_KERNEL", "ORACLE"
        }

        if (Os.isFamily(Os.FAMILY_UNIX) && "uname -a".execute().getText().contains("microsoft")) {
            excludeTags "FULL_LINUX_KERNEL"
        }
    }
}

configurations {
    freighterTestImplementation.extendsFrom testImplementation
    freighterTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    freighterTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    freighterTestImplementation "freighter:freighter-testing-core-junit5:0.4.5-SNAPSHOT"

    freighterTestImplementation project(":contracts")
    freighterTestImplementation project(":workflows")
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        allWarningsAsErrors = true
        languageVersion = "1.4"
        apiVersion = "1.4"
        jvmTarget = VERSION_11
        javaParameters = true   // Useful for reflection.
        freeCompilerArgs += [
                "-Xjvm-default=all",
                // Prevent Kotlin from warning about kotlin.* classes inside the OSGi bundle.
                "-Xskip-runtime-version-check",
                "-java-parameters"
        ]
    }
}


