apply plugin: 'kotlin-jpa'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.cordapp'


evaluationDependsOn(":modules:selection")

def selectionProject = project(":modules:selection")

cordapp {
    targetPlatformVersion 5
    minimumPlatformVersion 5
    workflow {
        name "Token SDK Workflows"
        vendor "R3"
        licence "Apache 2"
        versionId 2
    }
    signing {
        enabled false
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

compileKotlin{
    dependsOn (selectionProject.jar)
}

dependencies {
    //implementation 'android.arch.persistence.room:runtime:1.1.1'
    implementation "org.liquibase:liquibase-core:$liquibaseVersion"
    implementation "org.hibernate:hibernate-core:$hibernateVersion"

    // Kotlin.
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'

    // Corda dependencies.
    cordaCompile("$corda_release_group:corda-application:$corda_release_version") {
        changing = true
    }

    cordaCompile("$corda_release_group:corda-node-api:$corda_release_version") {
        changing = true
    }

    cordaCompile("$corda_release_group:corda-ledger:$corda_release_version") {
        changing = true
    }

    cordaCompile("$corda_release_group:corda-flows:$corda_release_version") {
        changing = true
    }

    // Logging.
    testCompile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"

    // Testing.
    testCompile "$corda_release_group:corda-node-driver:$legacy_corda_release_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "junit:junit:$junit_version"

    // CorDapp dependencies.
    cordapp project(":contracts")

    compile(files(selectionProject.jar.archivePath))

    //CI for confidential tokens
    cordapp "$confidential_id_release_group:ci-workflows:$confidential_id_release_version"

    //just for testing - this means you MUST specify the contracts-for-testing cordapp as a cordapp in all tests using test flows
    compileOnly project(":modules:contracts-for-testing")
    testCompile project(":modules:contracts-for-testing")

    compileOnly("org.postgresql:postgresql:42.2.8")

}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

jar {
    baseName "tokens-workflows"
}